#compdef man

setopt localoptions rcexpandparam extendedglob

local rep expl star approx

if [[ $compstate[pattern_match] != [^*] ]]; then
  # If a string other than *, we just want correction, so no `*'.
  star='*'
fi

if [[ -n $_comp_correct ]]; then
  # If this is set, we are correcting with this many approximations.
  approx="(#a${_comp_correct})"
fi

# `sman' is the SGML manual directory for Solaris 7.

if [[ $words[2] = (<->*|ln) ]]; then
  rep=(
  $manpath/(sman|man|cat)${words[2]}/${~approx}$PREFIX${~star}$SUFFIX.<->*(N:t) )
else
  rep=( $manpath/(sman|man|cat)*/${~approx}$PREFIX${~star}$SUFFIX.<->*(N:t) )
fi

_description expl 'manual page'
(( $#rep )) && compadd "$expl[@]" - ${rep%%.[^.]##(.gz|)}
