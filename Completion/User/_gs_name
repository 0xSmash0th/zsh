#autoload

local expl

if [[ "$1" = -d ]]; then
  if [[ "$PREFIX" = *\=* ]]; then
    _message 'systemdict definition value'
  else
    _description expl 'systemdict definition name'
    compadd "$expl[@]" -M 'm:{a-z}={A-Z}' - \
            DISKFONTS NOCACHE NOBIND NODISPLAY NOPAUSE PLATFONTS SAFER \
            WRITESYSTEMDICT
  fi
elif compset -P '*\='; then
  case "$IPREFIX" in
  *DEVICE\=)
    _description expl 'ghostscript device'
    compadd "$expl[@]" - "${(@)${=${$(gs -h)##* devices:}%%Search path:*}:#}"
    ;;
  *OutputFile\=)
    _description expl 'output file'
    _files
    ;;
  *)
    _message 'systemdict value'
    return 1
  esac
else
  _description expl 'systemdict name'
  compadd "$expl[@]" -S\= -M 'm:{a-z}={A-Z}' - DEVICE OutputFile
fi
