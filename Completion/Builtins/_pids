#autoload

# If given the `-m <pattern>' option, this tries to complete only pids
# of processes whose command line match the `<pattern>'.

local out list expl match desc listargs args

_wanted processes expl 'process ID' || return 1

if [[ "$1" = -m ]]; then
  match="${2}*"
  shift 2
fi

zstyle -a ":completion:${curcontext}:ps" arguments args

out="$(command ps $args 2>/dev/null)"

if zstyle -T ":completion:${curcontext}:processes" verbose; then
  zstyle -a ":completion:${curcontext}:ps" list-arguments listargs
  (( $#listargs )) || listargs=( "$args[@]" )
  if [[ "$listargs" = "$args" ]]; then
    list=("${(@Mr:COLUMNS-1:)${(f@)out}[2,-1]:#[ 	]#${PREFIX}[0-9]#${SUFFIX}[ 	]*${~match}}")
  else
    list=("${(@Mr:COLUMNS-1:)${(f@)$(command ps $listargs 2>/dev/null)}[2,-1]:#[ 	]#${PREFIX}[0-9]#${SUFFIX}[ 	]*${~match}}")
  fi
  desc=(-ld list)
else
  desc=()
fi

compadd "$expl[@]" "$@" "$desc[@]" - \
    ${${${(M)${(f)"$(command ps $args 2>/dev/null)"}[2,-1]:#[ 	]#${PREFIX}[0-9]#${SUFFIX}[ 	]#*${~match}}## #}%% *}
