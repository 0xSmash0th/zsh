#compdef -p zf*

# Don't try any more completion after this.
_compskip=all

# Completion for zftp builtin and zf* functions.  The functions
# zfcd_match and zfget_match (used for old-style completion)
# need to be installed for remote file and directory completion to work.

local subcom expl

if [[ $words[1] = zftp ]]; then
  if [[ $CURRENT -eq 2 ]]; then
    _description expl sub-command
    compadd "$expl[@]" open params user login type ascii binary mode put \
      putat get getat append appendat ls dir local remote mkdir rmdir
    return
  fi
  subcom=$words[2]
else
  subcom=$words[1]
fi

case $subcom in
  *(cd|ls|dir))
    # complete remote directories; we could be smarter about hiding prefixes
    zfcd_match $PREFIX $SUFFIX
    _description expl 'remote directory'
    (( $#reply )) && compadd "$expl[@]" -S/ -q - $reply
    ;;

  *(get(|at)|gcp|delete|remote))
    # complete remote files
    zfget_match $PREFIX $SUFFIX
    _description expl 'remote file'
    (( $#reply )) && compadd "$expl[@]" -F fignore - $reply
    ;;

  *(put(|at)|pcp))
    # complete local files
    _files
    ;;

  *(open|anon|params))
    # complete hosts:  should do cleverer stuff with user names
    _hosts
    ;;

  *(goto|mark))
    # complete bookmarks.  First decide if ncftp mode is go.
    _description expl bookmark
    if [[ $words[2] = -*n* ]]; then
      if [[ -f ~/.ncftp/bookmarks ]]; then
        compadd "$expl[@]" - $(awk -F, 'NR > 2 { print $1 }' ~/.ncftp/bookmarks)
      fi
    else
      if [[ -f ${ZFTP_BMFILE:=${ZDOTDIR:-$HOME}/.zfbkmarks} ]]; then
        compadd "$expl[@]" - $(awk '{print $1}' $ZFTP_BMFILE)
      fi
    fi
    ;;

  *)
    # dunno... try ordinary completion after all.
    _compskip=''
    return 1
    ;;
esac
