#autoload

local opts opt type=file glob group

opts=()
group=()
while getopts "P:S:qr:R:W:F:J:V:X:f/g:M:12n" opt; do
  case "$opt" in
  /)      type="${type}dir"                   ;;
  g)      type="${type}glob"; glob="$OPTARG"  ;;
  [qn12]) opts=("$opts[@]" "-$opt"          ) ;;
  [JV])   group=(          "-$opt" "$OPTARG") ;;
  [^f])   opts=("$opts[@]" "-$opt" "$OPTARG") ;;
  esac
done

if [[ "$group[2]" = files ]]; then
  opts=("$opts[@]" "$group[@]")
  group=()
fi

case "$type" in
*dir*glob*|*glob*dir) _tags globbed-files all-files             ;;
*glob*)               _tags globbed-files directories all-files ;;
*dir*)                _tags directories all-files               ;;
*)                    _tags all-files                           ;;
esac

while _tags; do
  if _requested all-files; then
    if (( $#group )); then
      group[2]=all-files
      _setup all-files
    fi
    _path_files "$opts[@]" -f
    return
  elif _requested directories; then
    if _requested globbed-files; then
      if (( $#group )); then
        group[2]=globbed-files
	_setup globbed-files
      fi
      _path_files "$opts[@]" -/g "$glob" && return 0
    else
      if (( $#group )); then
        group[2]=directories
	_setup directories
      fi
      _path_files "$opts[@]" -/ && return 0
    fi
  elif _requested globbed-files; then
    if (( $#group )); then
      group[2]=globbed-files
      _setup globbed-files
    fi
    if [[ "$type" = (*dir*glob*|*glob*dir*) ]]; then
      _path_files "$opts[@]" -/g "$glob" && return 0
    else
      _path_files "$opts[@]" -g "$glob" && return 0
    fi
  fi
done

return 1
