#autoload

local opts opt type=file

opts=()
while getopts "P:S:qr:R:W:F:J:V:X:f/g:M:" opt; do
  case "$opt" in
  /)    [[ "$type" = file       ]] && type=dir       ;;
  g)    [[ "$type" = (file|dir) ]] && type="$OPTARG" ;;
  q)    opts=("$opts[@]" -q               )          ;;
  [^f]) opts=("$opts[@]" "-$opt" "$OPTARG")          ;;
  esac
done

case "$type" in
file) _tags any all-files                           ;;
dir)  _tags any directories all-files               ;;
*)    _tags any globbed-files directories all-files ;;
esac

while _tags; do
  if _requested all-files; then
    _path_files "$opts[@]" -f
    return
  elif _requested directories; then
    if _requested globbed-files; then
      _path_files "$opts[@]" -/g "$type" && return 0
    else
      _path_files "$opts[@]" -/ && return 0
    fi
  elif _requested globbed-files; then
    _path_files "$opts[@]" -g "$type" && return 0
  fi
done

return 1
