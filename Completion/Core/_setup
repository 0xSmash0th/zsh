#autoload

local val nm="$compstate[nmatches]"

if _style -a "$1" list-colors val; then
  if [[ "$1" = default ]]; then
    ZLS_COLORS="${(j.:.)${(@)val:gs/:/\\\:}}"
  else
    eval "ZLS_COLORS=\"(${1})\${(j.:(${1}).)\${(@)val:gs/:/\\\:}}:\${ZLS_COLORS}\""
  fi
fi

if _style -s "$1" list-packed val; then
  if [[ "$val" = (yes|true|1|on) ]]; then
    compstate[list]="${compstate[list]} packed"
  else
    compstate[list]="${compstate[list]:gs/packed//}"
  fi
else
  compstate[list]="$_saved_list"
fi

if _style -s "$1" list-rows-first val; then
  if [[ "$val" = (yes|true|1|on) ]]; then
    compstate[list]="${compstate[list]} rows"
  else
    compstate[list]="${compstate[list]:gs/rows//}"
  fi
else
  compstate[list]="$_saved_list"
fi

if _style -s "$1" last-prompt val; then
  if [[ "$val" = (yes|true|1|on) ]]; then
    compstate[last_prompt]=yes
  else
    compstate[last_prompt]=''
  fi
else
  compstate[last_prompt]="$_saved_lastprompt"
fi

if _style -s "$1" accept-exact val; then
  if [[ "$val" = (yes|true|1|on) ]]; then
    compstate[exact]=accept
  else
    compstate[exact]=''
  fi
else
  compstate[exact]="$_saved_exact"
fi

[[ _last_nmatches -ge 0 && _last_nmatches -ne nm ]] &&
    _menu_style=( "$_last_menu_style[@]" "$_menu_style[@]" )

if _style -a "$1" menu val; then
  _last_nmatches="$nm"
  _last_menu_style=( "$val[@]" )
else
  _last_nmatches=-1
fi
