#autoload

# Use ignored matches.

(( $compstate[ignored] )) || return 1

local curcontext="${curcontext/:[^:]#:/:ignored-${(M)#_completers[1,_completer_num]:#_ignored}:}"
local comp i _comp_no_ignore=yes tmp expl

zstyle -a ":completion:${curcontext}:" completer comp ||
  comp=( "${(@)_completers[1,_completer_num-1][(R)_ignored,-1]}" )

for i in "$comp[@]"; do
  if [[ "$i" != _ignored ]] && "$i"; then
    if zstyle -s ":completion:${curcontext}:" single-ignored tmp &&
       [[ $compstate[old_list] != shown && $compstate[nmatches] -eq 1 ]]; then
      case "$tmp" in
      show) compstate[insert]='' compstate[list]='list force' tmp='' ;;
      menu)
        compstate[insert]=menu
        _description original expl original    
        compadd "$expl[@]" -S '' - "$PREFIX$SUFFIX"
        ;;
      *) tmp='' ;;
      esac
    fi

    return 0
  fi
done

return 1
