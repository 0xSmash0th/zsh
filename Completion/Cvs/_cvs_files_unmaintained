#autoload

if (( ! $+_cvs_ignore_default )); then
  _cvs_ignore_default=(
    RCS SCCS CVS CVS.adm RCSLOG 'cvslog.*' tags TAGS .make.state .nse_depinfo
    '*\~' '\#*' '.\#*' ',*' '_$*' '*$' '*.old' '*.bak' '*.BAK' '*.orig' '*.rej'
    '.del-*' '*.a' '*.olb' '*.o' '*.obj' '*.so' '*.exe' '*.Z' '*.elc' '*.ln'
    core
  )
fi

local qpref pref entries
_cvs_setup_prefix
if [[ -d ${pref}CVS ]]; then
  _cvs_setup_allentries
  setopt localoptions unset
  local omit
  omit=($_cvs_ignore_default ${entries:q} ${=cvsignore})
  [[ -r ~/.cvsignore ]] && omit=($omit $(<~/.cvsignore))
  [[ -r ${pref}.cvsignore ]] && omit=($omit $(<${pref}.cvsignore))
  compgen "$@" -g '*~(*/|)('${(j:|:)omit}')(D)' ||
  compgen "$@" -g '*~(*/|)('${(j:|:)${(@)entries:q}}')(D)' ||
  _cvs_directories "$@"
else
  _files "$@"
fi
