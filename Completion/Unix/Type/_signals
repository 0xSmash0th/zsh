#autoload

# Options (only in separate words):
#
# -a  use all signals (even the pseudo-signals)
# -p  needs a `-' prefix
#
# A `-' or `--' as the first argument is ignored.

local expl last minus

zparseopts -D -K -E 'p=minus' 'a=last'
if [[ -z "$last" ]]; then
  last=-1
else
  last=-3
fi
[[ -n "$minus" ]] && minus='-'

[[ "$1" = -(|-) ]] && shift

if [[ -z "$minus" ]] ||
   ! zstyle -T ":completion:${curcontext}:signals" prefix-needed ||
   [[ -prefix -* ]]; then
  local disp tmp

  [[ -prefix ${minus}SIG* ]] && minus+=SIG
  if zstyle -t ":completion:${curcontext}:signals" prefix-hidden; then
    tmp=( "${(@)signals[1,last]}" )
    disp=(-d tmp)
  else
    disp=()
  fi
  _wanted signals expl signal \
      compadd "$@" "$disp[@]" -M 'm:{a-z}={A-Z}' - \
              "${minus}${(@)^signals[1,last]}"
fi
