#compdef ffmpeg

local context state line expl
typeset -A opt_args

(( $+functions[_ffmpeg_presets] )) || _ffmpeg_presets() {
    local presets
    presets=(~/.ffmpeg/*.ffpreset(:t:r) "$FFMPEG_DATADIR"/*.ffpreset(:t:r))
    _wanted ffmpeg-presets expl 'select preset' compadd -a presets
}

(( $+functions[_ffmpeg_acodecs] )) || _ffmpeg_acodecs() {
    local acodecs
    acodecs=(copy ${${(M)${(f)"$(_call_program audio-codecs $words[1] -codecs 2>/dev/null)"}:#[[:space:]][D[:space:]][E[:space:]]A[S[:space:]][D[:space:]][T[:space:]][[:space:]][^[:space:]]##*}//(#b)????????([^[:space:]]##)*/$match[1]})
    _wanted ffmpeg-audio-codecs expl 'force audio codec (''copy'' to copy stream)' compadd -a acodecs
}

(( $+functions[_ffmpeg_vcodecs] )) || _ffmpeg_vcodecs() {
    local vcodecs
    vcodecs=(copy ${${(M)${(f)"$(_call_program video-codecs $words[1] -codecs 2>/dev/null)"}:#[[:space:]][D[:space:]][E[:space:]]V[S[:space:]][D[:space:]][T[:space:]][[:space:]][^[:space:]]##*}//(#b)????????([^[:space:]]##)*/$match[1]})
    _wanted ffmpeg-video-codecs expl 'force video codec (''copy'' to copy stream)' compadd -a vcodecs
}

(( $+functions[_ffmpeg_formats] )) || _ffmpeg_formats() {
    local formats
    formats=(${(ou)${=${(s:,:)${${(M)${(f)"$(_call_program formats $words[1] -formats 2>/dev/null)"}:#[[:space:]][D[:space:]][E[:space:]][[:space:]][^[:space:]]##*}//(#b)????([^[:space:]]##)*/$match[1]}}}})
    _wanted ffmpeg-formats expl 'force format' compadd -a formats
}

(( $+functions[_ffmpeg_list_pix_fmts] )) || _ffmpeg_list_pix_fmts() {
    echo - ${${${(M)${(f)"$(_call_program formats $words[1] -pix_fmts 2>/dev/null)"}:#[I.][O.][H.][P.][B.] [^=[:space:]]*}#* }%% *}
}

(( $+functions[_ffmpeg_pix_fmts] )) || _ffmpeg_pix_fmts() {
    local pix_fmts
    pix_fmts=($(_ffmpeg_list_pix_fmts))
    _wanted ffmpeg-pix-fmts expl 'set pixel format' compadd -a pix_fmts
}

(( $+functions[_ffmpeg_bsfs] )) || _ffmpeg_bsfs() {
    local bsfs
    bsfs=(${${(f)"$(_call_program bsfs $words[1] -bsfs 2>/dev/null)"}:#*:})
    _wanted ffmpeg-bsfs expl 'set bitstream filter' compadd -a bsfs
}

local -a _ffmpeg_argspecs
{
    local lastopt
    local lastopt_description
    local lastopt_takesargs
    local -a lastopt_values

    _call_program options $words[1] -h 2>/dev/null | while IFS=$'\n' read -r; do
        if [[ $REPLY == -* ]]; then
            if [[ -n $lastopt ]]; then
                if (( lastopt_takesargs )); then
                    lastopt+=":$lastopt_description:"
                    if (( $#lastopt_values )); then
                        lastopt+="(${lastopt_values[*]})"
                    fi
                fi
                _ffmpeg_argspecs+=$lastopt
            fi
            lastopt=${REPLY%%[[:space:]]*}
            lastopt_description=${REPLY##-[^[:space:]]##[[:space:]]##}
            if [[ $lastopt_description == '<'* ]]; then
                lastopt_description=${lastopt_description##<[^[:space:]]##>[[:space:]]##[^[:space:]]##[[:space:]]#}
                if [[ -z $lastopt_description ]]; then
                    lastopt_description=$lastopt
                fi
                lastopt_description=${lastopt_description//:/\\:}
            elif [[ $lastopt_description == [^[:space:]]##[[:space:]][[:space:]]* ]]; then
                local example=${lastopt_description%% *}
                example=${example//:/\\:}
                lastopt_description=${lastopt_description##[^[:space:]]##[[:space:]]##}
                lastopt_description=${lastopt_description//:/\\:}
                if [[ $example == filename ]]; then
                    lastopt_takesargs=0
                    lastopt+=":$lastopt_description:_files"
                elif [[ $lastopt == -[asv]pre ]]; then
                    lastopt_takesargs=0
                    lastopt+=": :_ffmpeg_presets"
                elif [[ $lastopt == -acodec ]]; then
                    lastopt_takesargs=0
                    lastopt+=": :_ffmpeg_acodecs"
                elif [[ $lastopt == -vcodec ]]; then
                    lastopt_takesargs=0
                    lastopt+=": :_ffmpeg_vcodecs"
                elif [[ $lastopt == -f ]]; then
                    lastopt_takesargs=0
                    lastopt+=": :_ffmpeg_formats"
                elif [[ $lastopt == -pix_fmt ]]; then
                    lastopt_takesargs=0
                    lastopt+=": :_ffmpeg_pix_fmts"
                elif [[ $example == bitstream_filter ]]; then
                    lastopt_takesargs=0
                    lastopt+=": :_ffmpeg_bsfs"
                else
                    lastopt_takesargs=1
                    lastopt_description+=" ($example)"
                fi
            else
                lastopt_takesargs=0
                if [[ $lastopt == -vfilters ]]; then
                    lastopt+=": :->vfilters"
                fi
            fi
            lastopt_values=()
        elif [[ $REPLY == ' '* ]]; then
            REPLY=${REPLY##[[:space:]]##}
            REPLY=${REPLY%%[[:space:]]##*}
            lastopt_takesargs=1
            lastopt_values+=$REPLY
        fi
    done
    if [[ -n $lastopt ]]; then
        if (( lastopt_takesargs )); then
            lastopt+=":$lastopt_description:"
            if (( $#lastopt_values )); then
                lastopt+="(${lastopt_values[*]})"
            fi
        fi
        _ffmpeg_argspecs+=$lastopt
    fi
}

_arguments -S \
    "${_ffmpeg_argspecs[@]}" \
    '*:output file:_files' \
    && return 0

[[ "$state" == "vfilters" ]] &&
    _values -s , -S = 'video filters' \
    'aspect:set aspect ratio (rational number X\:Y or decimal number):' \
    'crop:crop input video (x\:y\:width\:height):' \
    'format: :->format' \
    'noformat: :->noformat' \
    'null' \
    'pad:add pads to the input image (width\:height\:x\:y\:color_string):' \
    'pixelaspect:set pixel aspect ratio (rational number X\:Y or decimal number):' \
    'scale:scale input video (width\:height):' \
    'slicify:output slice height ("random" or a number of pixels):' \
    'unsharp:luma_x\:luma_y\:luma_amount\:chroma_x\:chroma_y\:chroma_amount:' \
    'vflip' \
    'buffer' \
    'nullsrc' \
    'nullsink' \
    && return 0

[[ "$state" == "format" ]] &&
    _values -s : -S = 'convert input video to one of the specified pixel formats' $(_ffmpeg_list_pix_fmts) && return 0

[[ "$state" == "noformat" ]] &&
    _values -s : -S = 'disable specified pixel formats by force' $(_ffmpeg_list_pix_fmts) && return 0

return 1
