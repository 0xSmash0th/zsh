#compdef getopts read unset vared

# This will handle completion of keys of associative arrays, e.g. at
# `vared compconfig[<TAB>'.  However, in this version the [ must be
# added by hand.

if [[ $PREFIX = *\[* ]]; then
  local var=${PREFIX%%\[*}
  local elt="${PREFIX#*\]}${SUFFIX%\]}"
  local addclose

  compset -p $(( ${#var} + 1 ))
  if ! compset -S \]; then
    addclose=(-S ']')
  fi
  if [[ ${(tP)var} = assoc* ]]; then
    local expl

    _tags subscript association-keys || return 1

    _description expl 'association key'
    compadd "$expl[@]" $addclose - ${(kP)var}
  fi
else
  _tags any parameters || return 1

  _parameters
fi
