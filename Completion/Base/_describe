#autoload

# This can be used to add options or values with descriptions as matches.

local _cmd _opt _expl _tmps _tmpd _tmpmd _tmpms _ret=1 _showd _nm _hide _args
local _type=values

_cmd="$words[1]"

# Get the options.

while getopts 'oc:' _opt; do
  if [[ "$_opt" = o ]]; then
    _type=options
  else
    _cmd="$OPTARG"
  fi
done
shift OPTIND-1

# Do the tests. `showd' is set if the descriptions should be shown.

_tags "$_type" || return 1

_style "$_type" description && _showd=yes

_description _expl "$1"
shift

if [[ -n "$_showd" ]]; then
  compdescribe -I ' -- ' "$@"
else
  compdescribe -i "$@"
fi

[[ "$_type" = options ]] && _style options prefix-hidden && _hide=yes

while compdescribe -g _args _tmpd _tmpmd _tmps _tmpms; do

  # See if we should remove the option prefix characters.

  if [[ -n "$_hide" ]]; then
    if [[ "$PREFIX" = --* ]]; then
      _tmpd=( "${(@)_tmpd#--}" )
      _tmps=( "${(@)_tmps#--}" )
    elif [[ "$PREFIX" = [-+]* ]]; then
      _tmpd=( "${(@)_tmpd#[-+]}" )
      _tmps=( "${(@)_tmps#[-+]}" )
    fi
  fi
  compadd "$_args[@]" "$_expl[@]" -ld _tmpd - "$_tmpmd[@]" && _ret=0
  compadd "$_args[@]" "$_expl[@]" -d _tmps  - "$_tmpms[@]" && _ret=0
done

return _ret
