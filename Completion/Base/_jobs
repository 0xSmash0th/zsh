#autoload

local expl disp jobs job jids pfx='%' desc

_tags any jobs || return 1

_style jobs prefix-needed yes && [[ "$PREFIX" != %* ]] && return 1
_style jobs prefix-hidden yes && pfx=''
_style jobs description yes   && desc=yes

if [[ "$1" = -r ]]; then
  jids=( "${(@k)jobstates[(R)running*]}" )
  shift
  _description expl 'running job'
elif [[ "$1" = -s ]]; then
  jids=( "${(@k)jobstates[(R)running*]}" )
  shift
  _description expl 'suspended job'
else
  [[ "$1" = - ]] && shift
  jids=( "${(@k)jobtexts}" )
  _description expl job
fi

disp=()
jobs=()
for job in "$jids[@]"; do
  [[ -n "$desc" ]] &&
      disp=( "$disp[@]" "${pfx}${(r:2:: :)job} -- ${(r:COLUMNS-8:: :)jobtexts[$job]}" )
  jobs=( "$jobs[@]" "$job" )
done

if [[ -n "$desc" ]]; then
  compadd "$@" "$expl[@]" -ld disp - "%$^jobs[@]"
else
  compadd "$@" "$expl[@]" - "%$^jobs[@]"
fi
