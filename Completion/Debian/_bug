#compdef bug reportbug

_bug_commonargs=('-d[debug: send mail to postmaster@localhost]' \
           '-m[maintainer-only]' \
           '-p[print to stdout instead of mail]' \
           '-q[quiet - no e-mail forwarding]' \
           '-h[help]' \
           '-v[version]' \
           '*:package:_deb_packages installed')

case "$words[1]" in
bug)
_arguments '-c[exclude configs from report]' \
           '-f[argument is a file, not a package]' \
           '-H[special header]:custom header:' \
           '-s[set subject]:subject:' \
           '-S[set severity]:severity:(wishlist normal important grave critical)' \
           '-x[do not cc submitter]' \
           '-z[send configs verbatim]' \
           "$_bug_commonargs[@]"
;;

reportbug)
_arguments '(--no-config-files)-c[exclude configs from report]' \
           '(--file=)-f[argument is a file, not a package]:filename:' \
           '(--header=)-H[special header]:custom header:' \
           '(--subject=)-s[set subject]:subject:' \
           '(--severity=)-S[set severity]:severity:(wishlist normal important grave critical)' \
           '(--no-cc)-x[do not cc submitter]' \
           '(--no-compress)-z[send configs verbatim]' \
           '(--af)-a[use af instead of editor]' \
           '(--no-bts-query)-b[do not check bts]' \
           '(--bts=)-B[use alternate BTS]:system:(debian gnome kde tdyc)' \
           '-g[sign report with GnuPG]' \
           '(--include=)-i[include text]:' \
           '(--no-ldap)-l[disable LDAP support]' \
           '(--mutt)-M[use mutt instead of editor]' \
           '--mua=[use specified mua instead of editor]' \
           '(--nmh --mh)-n[use comp instead of editor]' \
           '(--output=)-o[output to file instead of mail]' \
           '(--pgp)-P[sign report with PGP]' \
           '--ldap[enable LDAP support]' \
           "$_bug_commonargs[@]"
;;
esac
