#compdef module-assistant m-a

_arguments \
 '(-h --help)'{-h,--help}'[print help screen]' \
 '(-v --verbose)'{-v,--verbose}'[Be verbose, show full paths, etc.]' \
 '(-q --quiet)'{-q,--quiet}'[The opposite of verbose]' \
 '(-n --no-rebuild)'{-n,--no-rebuild}'[Do not rebuild when package exists]' \
 '(-i --ignore-failures)'{-i,--ignore-failures}'[Do not stop on build failures]' \
 '(-s --apt-search)'{-s,--apt-search}'[Search for installation candidates in the Debian archive]' \
 '(-f --force)'{-f,--force}'[Force using new versions even when old ones exist]' \
 '(-u --userdir)'{-u,--userdir}'[Specify a (writable) replacement directory for /var&/usr]:dir:_files -/' \
 '(-l --kvers-list)'{-l,--kvers-list}'[List of kernel versions to work on (default: current version)]:kernel version list:' \
 '(-k --kernel-dir)'{-k,--kernel-dir}'[List of kernel headers/source directories, comma separated]:list of dirs:_files -/' \
 '(-t --text-mode)'{-t,--text-mode}'[no progress bars]' \
 '1:list of commands:_values -s , subcommands update get build list install auto-install prepare clean purge' \
 '*:packages: compadd ${${${$(ls /usr/src/modass/var_cache_modass/*.avail_version)}:t}%.avail_version}'

